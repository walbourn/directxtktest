@echo off
setlocal
REM Copyright (c) Microsoft Corporation.
REM Licensed under the MIT License.

where /Q Microsoft.CodeCoverage.Console.exe
@if errorlevel 1 goto notool

set BINDIR=%~dp0
cd /d %BINDIR%

set "FILES="

REM apitest
Microsoft.CodeCoverage.Console instrument --settings coverage-bvt.config apitest.exe
pushd ..\..\..\..\Tests
Microsoft.CodeCoverage.Console collect %BINDIR%\apitest.exe
@if errorlevel 1 goto failure
call set "FILES=%%FILES%% output_apitest.coverage"
move output.coverage %BINDIR%\output_apitest.coverage
popd

REM ddswictest
Microsoft.CodeCoverage.Console instrument --settings coverage-bvt.config ddswictest.exe
pushd ..\..\..\..\Tests
Microsoft.CodeCoverage.Console collect %BINDIR%\ddswictest.exe
@if errorlevel 1 goto failure
call set "FILES=%%FILES%% output_ddswictest.coverage"
move output.coverage %BINDIR%\output_ddswictest.coverage
popd

Microsoft.CodeCoverage.Console merge %FILES%
@if errorlevel 1 goto failure

echo Open output.coverage using the Visual Studio IDE to view it.
exit /b 0

:failure
echo *** Microsoft.CodeCoverage.Console failed
exit /b 1

:pushfail
echo *** pushd failed
exit /b 1

:notool
ECHO Code-coverage using MSVC requires Microsoft.CodeCoverage.Console in Visual Studio Enterprise
exit /b 1
