@echo off
setlocal
REM Copyright (c) Microsoft Corporation.
REM Licensed under the MIT License.

where /Q Microsoft.CodeCoverage.Console.exe
@if errorlevel 1 goto notool

set BINDIR=%~dp0
cd /d %BINDIR%

set "FILES="

for %%1 in (d3d11test msaatest effectstest primitivestest spritebatchtest spritefonttest loadtest modeltest shadertest animtest dgsltest hdrtest pbrtest pbrmodeltest postprocesstest) do (Microsoft.CodeCoverage.Console instrument --settings coverage.config .\%%1.exe
pushd ..\..\..\..\Tests\%%1
@if errorlevel 1 goto pushfail
Microsoft.CodeCoverage.Console collect %BINDIR%\%%1.exe -ctest
@if errorlevel 1 goto failure
call set "FILES=%%FILES%% output_%%1.coverage"
move output.coverage %BINDIR%\output_%%1.coverage
popd)

Microsoft.CodeCoverage.Console merge %FILES%
@if errorlevel 1 goto failure

echo Open output.coverage using the Visual Studio IDE to view it.
exit /b 0

:failure
echo *** Microsoft.CodeCoverage.Console failed
exit /b 1

:pushfail
echo *** pushd failed
exit /b 1

:notool
ECHO Code-coverage using MSVC requires Microsoft.CodeCoverage.Console in Visual Studio Enterprise
exit /b 1
